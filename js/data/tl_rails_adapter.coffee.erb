siteRoot ?= ''

TL.RailsAdapter = DS.RESTAdapter.extend

  # Override the pluralize method in DS.RESTAdapter with
  # code from underscore.inflection.js
  pluralize: (name) -> _.pluralize(name)


  findAll: (store, type) ->
    root = @rootForType(type)
    relURL = type.url or @pluralize(root)

    url = "#{siteRoot}/#{relURL}"

    @ajax url, "GET",
      success: (json) => @findAllCallback?(store, type, json)
      error: @_ajaxError


  createRecords: (store, type, models) ->
    models.forEach (model) ->
      @createRecord(store, type, model)
    , @


  updateRecords: (store, type, models) ->
    models.forEach (model) ->
      @updateRecord(store, type, model)
    , @


  deleteRecords: (store, type, models) ->
    models.forEach (model) ->
      @deleteRecord(store, type, model)
    , @


  createRecord: (store, type, model) ->
    root = @rootForType(type)
    relURL = type.url or @pluralize(root)

    url = "#{siteRoot}/#{relURL}"

    @ajax url, "POST",
      data: model.toJSON()
      contentType: 'application/json'
      success: (json) => @createRecordCallback?(store, type, model, json)
      error: @_ajaxError


  updateRecord: (store, type, model) ->
    primaryKey = Em.getPath(type, 'proto.primaryKey')
    id = model.get(primaryKey)
    root = @rootForType(type)
    relURL = type.url or @pluralize(root)

    url = "#{siteRoot}/#{relURL}/#{id}"

    @ajax url, "PUT",
      data: model.toJSON()
      contentType: 'application/json'
      success: (json) => @updateRecordCallback?(store, type, model, json)
      error: @_ajaxError


  deleteRecord: (store, type, model) ->
    primaryKey = Em.getPath(type, 'proto.primaryKey')
    id = model.get(primaryKey)
    root = @rootForType(type)
    relURL = type.url or @pluralize(root)

    url = "#{siteRoot}/#{relURL}/#{id}"

    @ajax url, "DELETE",
      success: (json) => @deleteRecordCallback?(store, type, model, json)
      error: @_ajaxError


  _ajaxError: -> alert 'Server error. Please reload the web page and try again.'
